group = "no.nav.sokos"

val flywayVersion = "11.20.2"
val hikariVersion = "7.0.2"
val ibmMqVersion = "9.4.4.1"
val janinoVersion = "3.1.12"
val jsonUnitVersion = "5.1.0"
val konfigVersion = "1.6.10.0"
val kotestVersion = "6.1.1"
val kotlinLoggingVersion = "3.0.5"
val kotlinxSerializationVersion = "1.10.0"
val kotliqueryVersion = "1.9.1"
val ktorVersion = "3.4.0"
val logbackVersion = "1.5.26"
val logstashVersion = "9.0"
val micrometerVersion = "1.16.2"
val mockOAuth2ServerVersion = "3.0.1"
val mockkVersion = "1.14.7"
val pdpClientVersion = "1.1.0"
val postgresVersion = "42.7.9"
val swaggerRequestValidatorVersion = "2.46.0"
val testcontainersVersion = "2.0.3"
val threetenExtraVersion = "1.8.0"
val tokenSupportVersion = "6.0.0"
val utilsVersion = "0.10.1"

plugins {
    val kotlinVersion = "2.3.0"

    application
    kotlin("jvm") version kotlinVersion
    kotlin("plugin.serialization") version kotlinVersion
    id("com.diffplug.spotless") version "8.1.0"
    id("org.jetbrains.kotlinx.kover") version "0.9.4"
}

application {
    mainClass = "no.nav.sokos.oppgjorsrapporter.ApplicationKt"
}

repositories {
    val githubPassword: String by project
    mavenCentral()
    maven {
        setUrl("https://maven.pkg.github.com/navikt/*")
        credentials {
            username = "x-access-token"
            password = githubPassword
        }
    }
}

dependencies {
    // Ktor server
    implementation("io.ktor:ktor-server-call-logging:$ktorVersion")
    implementation("io.ktor:ktor-server-content-negotiation-jvm:$ktorVersion")
    implementation("io.ktor:ktor-server-di:$ktorVersion")
    implementation("io.ktor:ktor-server-netty-jvm:$ktorVersion")
    implementation("io.ktor:ktor-server-swagger:$ktorVersion")

    // Ekstra tid/dato-klasser (utover java.time.*)
    implementation("org.threeten:threeten-extra:$threetenExtraVersion")

    // Ktor client
    implementation("io.ktor:ktor-client-content-negotiation:$ktorVersion")
    implementation("io.ktor:ktor-client-apache5-jvm:$ktorVersion")
    implementation("io.ktor:ktor-client-auth:${ktorVersion}")
    implementation("io.ktor:ktor-client-logging:${ktorVersion}")

    // Database
    implementation("com.zaxxer:HikariCP:$hikariVersion")
    implementation("org.postgresql:postgresql:$postgresVersion")
    implementation("com.github.seratch:kotliquery:$kotliqueryVersion")
    implementation("org.flywaydb:flyway-database-postgresql:$flywayVersion")

    // MQ
    implementation("com.ibm.mq:com.ibm.mq.allclient:${ibmMqVersion}")

    // Security
    implementation("no.nav.security:token-validation-ktor-v3:${tokenSupportVersion}")

    // Serialization
    implementation("io.ktor:ktor-serialization-kotlinx-json-jvm:$ktorVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:$kotlinxSerializationVersion")

    // Monitorering
    implementation("io.ktor:ktor-server-metrics-micrometer-jvm:$ktorVersion")
    implementation("io.micrometer:micrometer-registry-prometheus:$micrometerVersion")

    // Logging
    implementation("io.github.microutils:kotlin-logging-jvm:$kotlinLoggingVersion")
    runtimeOnly("org.codehaus.janino:janino:$janinoVersion")
    runtimeOnly("ch.qos.logback:logback-classic:$logbackVersion")
    runtimeOnly("net.logstash.logback:logstash-logback-encoder:$logstashVersion")

    // Inntil vi får tenkt gjennom om vi vil gjøre serialization etc. annerledes så henter vi inn Team HAG sine utils
    implementation("no.nav.helsearbeidsgiver:utils:${utilsVersion}")
    implementation("no.nav.helsearbeidsgiver:altinn-pdp-client:${pdpClientVersion}")
    testImplementation(testFixtures("no.nav.helsearbeidsgiver:utils:$utilsVersion"))

    // Test
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2")
    testImplementation("io.ktor:ktor-server-test-host-jvm:$ktorVersion")
    testImplementation("io.kotest:kotest-assertions-core-jvm:$kotestVersion")
    testImplementation("io.kotest:kotest-runner-junit5:$kotestVersion")
    testImplementation("io.kotest:kotest-extensions-testcontainers:$kotestVersion")
    testImplementation("io.ktor:ktor-client-mock:${ktorVersion}")
    testImplementation("io.mockk:mockk:$mockkVersion")
    testImplementation("no.nav.security:mock-oauth2-server:$mockOAuth2ServerVersion")
    testImplementation("com.atlassian.oai:swagger-request-validator-restassured:${swaggerRequestValidatorVersion}")
    testImplementation("org.testcontainers:testcontainers-postgresql:$testcontainersVersion")
    testImplementation("com.ibm.mq:mq-java-testcontainer:2.0.3")
    testImplementation("net.javacrumbs.json-unit:json-unit:${jsonUnitVersion}")
    testImplementation("net.javacrumbs.json-unit:json-unit-assertj:${jsonUnitVersion}")
}

kotlin {
    jvmToolchain(21)
    compilerOptions {
        extraWarnings = true
        // extraWarnings would be too noisy on the files generated by the GraphQL plugin, as they have explicit "public" modifiers.
        // As a workaround for that, disable the "REDUNDANT_VISIBILITY_MODIFIER" warning globally:
        freeCompilerArgs.add("-Xwarning-level=REDUNDANT_VISIBILITY_MODIFIER:disabled")
        // Use Kotlin 2.2's experimental new defaulting rules for annotation targets - see
        // https://kotlinlang.org/docs/whatsnew22.html#new-defaulting-rules-for-use-site-annotation-targets
        freeCompilerArgs.add("-Xannotation-default-target=param-property")
        freeCompilerArgs.add("-Xreturn-value-checker=full")
    }
}

tasks {
    withType<Test>().configureEach {
        useJUnitPlatform()

        testLogging {
            events("passed", "skipped", "failed")
            showExceptions = true
            showStackTraces = true
            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        }

        reports.forEach { report -> report.required.value(false) }
    }

    ("build") {
        dependsOn("copyPreCommitHook")
    }

    register<Copy>("copyPreCommitHook") {
        from(".scripts/pre-commit")
        into(".git/hooks")
        filePermissions {
            user {
                execute = true
            }
        }
        doFirst {
            println("Installing git hooks...")
        }
        doLast {
            println("Git hooks installed successfully.")
        }
        description = "Copy pre-commit hook to .git/hooks"
        group = "git hooks"
        outputs.upToDateWhen { false }
    }
}

spotless {
    kotlin {
        targetExclude("src/main/java/**/*")
        targetExclude("build/generated/**/*")
        ktfmt().kotlinlangStyle().configure {
            it.setMaxWidth(140)
            it.setRemoveUnusedImports(true)
            it.setTrailingCommaManagementStrategy(com.diffplug.spotless.kotlin.KtfmtStep.TrailingCommaManagementStrategy.COMPLETE)
        }
    }
}
